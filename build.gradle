/*ยง
  ===========================================================================
  LambdaPrism
  ===========================================================================
  Copyright (C) 2016 Gianluca Costa
  ===========================================================================
  Licensed under the Apache License, Version 2.0 (the "License");
  you may not use this file except in compliance with the License.
  You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

  Unless required by applicable law or agreed to in writing, software
  distributed under the License is distributed on an "AS IS" BASIS,
  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  See the License for the specific language governing permissions and
  limitations under the License.
  ===========================================================================
*/

buildscript {
    repositories {
        maven {
            url 'https://dl.bintray.com/giancosta86/Hephaestus'
        }

        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'info.gianlucacosta.moonlicense:moonlicense-gradle:4.3'
        classpath 'info.gianlucacosta.aurora:aurora:7.0'
        classpath 'com.github.maiflai:gradle-scalatest:0.12'
    }
}


apply plugin: 'scala'
apply plugin: 'antlr'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'osgi'
apply plugin: 'com.github.maiflai.scalatest'
apply plugin: 'info.gianlucacosta.moonlicense'
apply plugin: 'info.gianlucacosta.aurora'



group = 'info.gianlucacosta.lambdaprism'
archivesBaseName = 'lambdaprism'
version = '1.0'

description = 'Scala OOP-FP pragmatic library for Artificial Intelligence'



dependencies {
    generatedCompile 'org.scala-lang:scala-library:2.11.8'
    generatedCompile 'org.antlr:antlr4-runtime:4.5.3'

    compile 'org.scala-lang:scala-library:2.11.8'
    compile 'org.antlr:antlr4-runtime:4.5.3'

    compile 'info.gianlucacosta.omnieditor:omnieditor:4.0'
    compile 'info.gianlucacosta.helios:helios-core:3.0'

    testCompile 'org.scalatest:scalatest_2.11:2.2.6'
    testRuntime 'org.pegdown:pegdown:1.6.0'

    antlr 'org.antlr:antlr4:4.5.3'
}



aurora {
    gitHubUser = 'giancosta86'

    author {
        name = 'Gianluca Costa'
        email = 'gianluca@gianlucacosta.info'
        url = 'http://gianlucacosta.info/'
    }

    bintray {
        repo = 'Hephaestus'
        licenses = ['Apache-2.0']
        labels = [
                'Scala',
                'library',
                'artificial',
                'intelligence',
                'AI',
                'planning',
                'logic',
                'pattern',
                'matching',
                'functional',
                'OOP',
                'FP'
        ]
    }
}


moonLicense {
    license = apache2

    productInfo {
        productName = 'LambdaPrism'
        inceptionYear = 2016
        copyrightHolder = 'Gianluca Costa'
    }


    includes = includes + [
            /.*\.g4$/: javaFormat
    ]
}


generateGrammarSource {
    outputDirectory = project.file("src/generated/java/info/gianlucacosta/lambdaprism/planning/problem/parser")
    arguments += [
            '-visitor',
            '-long-messages',
            '-no-listener',
            '-Werror'
    ]
}
generateGrammarSource.dependsOn("cleanGenerated")

generateArtifactInfo.dependsOn("generateGrammarSource")
